
model {

  # -------------------
  # Detection model
  # -------------------
  alpha_Int ~ dunif(-5,5)    # overall intercept for log(sigma)

  # Random (partial-pooling) year effects for sigma
  for(t in 1:nYears){
    alpha_year[t] ~ dnorm(0, tau_year)    # year-level deviations (mean 0)
  }
  tau_year <- pow(sd_year, -2)
  sd_year ~ dunif(0, 5)   # weakly informative prior on year SD

  # -------------------
  # Abundance model
  # -------------------
  beta_Int ~ dunif(-5,5)           # overall abundance mean

  # New: year-specific additive effects for abundance (no pooling)
  for(t in 1:nYears){
    beta_year[t] ~ dunif(-5,5)     # additive effect of year t
    beta_fieldS[t] ~ dunif(-5,5)        # field covariate for each year t
  }

  # -------------------
  # Likelihood
  # -------------------
  for(i in 1:nind){
    dclass[i] ~ dcat(fc[site[i],]) # Part 1 of HM
  }

  for(s in 1:nsites){

    # Distance-sampling probabilities
    for(g in 1:nD){
      log(p[s,g]) <- -midpt[g] * midpt[g] / (2 * sigma[s] * sigma[s])
      pi[s,g] <- ((2 * midpt[g]) / (B * B)) * delta
      f[s,g] <- p[s,g] * pi[s,g]
      fc[s,g] <- f[s,g] / pcap[s]
    }
    pcap[s] <- sum(f[s,])

    ncap[s] ~ dbin(pcap[s], N[s])
    N[s] ~ dpois(lambda[s])

    # Abundance linear model with year-specific additive effects
    log(lambda[s]) <- beta_Int + beta_year[ yearIndex[s] ] +
                      beta_fieldS[ yearIndex[s] ] * field[s]

    # Detection linear model
    log(sigma[s]) <- alpha_Int + alpha_year[ yearIndex[s] ]

  } # end sites

  # Derived parameters
  
  # lambda: mean count per sample
  lam.S.Y10 <- exp(beta_Int + beta_fieldS[1] + beta_year[1])/4
  lam.K.Y10 <- exp(beta_Int + beta_year[1])/4
  lam.S.Y12 <- exp(beta_Int + beta_fieldS[2] + beta_year[2])/4
  lam.K.Y12 <- exp(beta_Int + beta_year[2])/4
  lam.S.Y13 <- exp(beta_Int + beta_fieldS[3] + beta_year[3])/4
  lam.K.Y13 <- exp(beta_Int + beta_year[3])/4
  lam.S.Y18 <- exp(beta_Int + beta_fieldS[4] + beta_year[4])/4
  lam.K.Y18 <- exp(beta_Int + beta_year[4])/4
  lam.S.Y19 <- exp(beta_Int + beta_fieldS[5] + beta_year[5])/4
  lam.K.Y19 <- exp(beta_Int + beta_year[5])/4
  lam.S.Y24 <- exp(beta_Int + beta_fieldS[6] + beta_year[6])/4
  lam.K.Y24 <- exp(beta_Int + beta_year[6])/4
  lam.S.Y25 <- exp(beta_Int + beta_fieldS[7] + beta_year[7])/4
  lam.K.Y25 <- exp(beta_Int + beta_year[7])/4
  
  # annual sigma for half-normal detection function
  sigma10 <- sigma[1]
  sigma12 <- sigma[20]
  sigma13 <- sigma[39]
  sigma18 <- sigma[58]
  sigma19 <- sigma[77]
  sigma24 <- sigma[96]
  sigma25 <- sigma[115]
}
